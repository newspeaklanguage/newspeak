Newspeak3
'Root'
class  HopscotchWebIDE packageUsing: manifest = (
	(* The Newspeak IDE in the web browser. *)
	|
	private Browsing = manifest Browsing.
	private CombinatorialParsing = manifest CombinatorialParsing.
	private Debugging = manifest Debugging.
	private DeploymentManager = manifest DeploymentManager.	
	private MetadataParsing = manifest MetadataParsing.	    
	private Minitest = manifest Minitest.
	private MinitestUI = manifest MinitestUI.
	private Namespacing = manifest Namespacing.
	private NewspeakColorization = manifest NewspeakColorization.
	private NewspeakGrammar = manifest NewspeakGrammar.
	private NewspeakASTs = manifest NewspeakASTs.	
    private NewspeakParsing = manifest NewspeakParsing.
    private Documents = manifest Documents.
    private DocumentHolder = manifest DocumentHolder.
	private WorkspaceHolder = manifest WorkspaceHolder.	
	private WorkspaceManager = manifest WorkspaceManager.
	private RuntimeForPrimordialSoup = manifest RuntimeForPrimordialSoup.
	private RuntimeWithMirrorsForPrimordialSoup = manifest RuntimeWithMirrorsForPrimordialSoup.
    private WebCompiler = manifest WebCompiler.
    
	(* Web stuff *)
    private WebFiles = manifest WebFiles.
	private JSON = manifest JSON.
	
	(* JS stuff *)
	private NewspeakCompilation = manifest NewspeakCompilation. 
    private JavascriptGeneration = manifest JavascriptGeneration.
	private Newspeak2JSCompilation = manifest Newspeak2JSCompilation.
	private ActorsForJS = manifest ActorsForJS.
	private AliensForJS = manifest AliensForJS.	
	private MirrorsForJS = manifest MirrorsForJS.
	private MirrorGroups = manifest MirrorGroups.	
	private KernelForJS = manifest KernelForJS.
	private Collections = manifest Collections.
	private Streams = manifest Streams.
    private RuntimeForHopscotchForHTML = manifest RuntimeForHopscotchForHTML.
	private RuntimeForJS = manifest RuntimeForJS.
	private RuntimeForJSWithMirrorBuilders = manifest RuntimeForJSWithMirrorBuilders.		
	private testModules = {
		manifest AccessModifierTesting.
		manifest AccessModifierTestingConfiguration.
		manifest KernelTests.
		manifest KernelTestsConfiguration.
		manifest MinitestTests.
		manifest MinitestTestsConfiguration.
		manifest MirrorTesting.
		manifest MirrorTestingConfiguration.
		manifest MirrorTestingModel.

manifest MirrorBuilderTesting.
		manifest MirrorBuilderTestingConfiguration.
		manifest ActivationMirrorTesting.
		manifest ActivationMirrorTestingConfiguration.
		manifest JSTesting.
		manifest JSTestingConfiguration.
	}.
	
(* private imagePaths = HopscotchImagePaths packageUsing: manifest.*)

    private standardNames <Set[String]> 
    private standardIconNames <Set[String]> 
	|
	(* manifest codemirror.*)
) (
class HopscotchImagePaths packageUsing: manifest = (
	| (* We expect the manifest to store paths that can be converted into usable obejcts at runtime. Eventually these should become Resource objects with more general properties. *)
	private addImage_resource = manifest addImage.
        private ampleforthDocument_resource = manifest ampleforthDocument.
	private classImage_resource = manifest classImage.
	private classPresenterImage_resource = manifest classPresenterImage.
	private classUnknownImage_resource = manifest classUnknownImage.
	private collapseImage_resource = manifest collapseImage.
	private expandImage_resource = manifest expandImage.
	private privateAccessImage_resource = manifest privateImage.
	private protectedAccessImage_resource = manifest protectedImage.
	private publicAccessImage_resource = manifest publicImage.
        private saveImage_resource = manifest saveImage.
        private sectionImage_resource = manifest sectionImage.
  |) (
) : (
)
public class HopscotchImages usingPlatform: p = (
  (* This class stores the actual image objects provided by the UI; The images are lazily computed based on their paths.
  *)
  | private JsImage = platform js global at: 'Image'. |
) (
lazy public addImage = resolveImage: imagePaths addImage_resource.
lazy public ampleforthDocument = resolveImage: imagePaths ampleforthDocument_resource.
lazy public classImage = resolveImage: imagePaths classImage_resource.
lazy public classPresenterImage = resolveImage: imagePaths classPresenterImage_resource.
lazy public classUnknownImage = resolveImage: imagePaths classUnknownImage_resource.
lazy public collapseImage = resolveImage: imagePaths collapseImage_resource.
lazy public expandImage = resolveImage: imagePaths expandImage_resource.
lazy public privateAccessImage = resolveImage: imagePaths privateImage_resource.
lazy public protectedAccessImage = resolveImage: imagePaths protectedImage_resource.
lazy public publicAccessImage = resolveImage: imagePaths publicImage_resource.
lazy public saveImage = resolveImage: imagePaths saveImage_resource.
lazy public sectionImage = resolveImage: imagePaths sectionImage_resource.
resolveImage: path <String> ^ <Alien[Image]> = (^JsImage new at: 'src' put: path; yourself)
) : (
)
class RestoreDialog usingPlatform: p = (
  |
  private List = p collections List.
  private Presenter = p hopscotch Presenter.
  private Subject = p hopscotch Subject.
  window = p hopscotch HopscotchWindow openSubject: (RestoreDialogSubject onModel: p).
  |
) (
public class RestoreDialogSubject onModel: p = Subject onModel: p () (
public isKindOfRestoreDialogSubject ^ <Boolean> = (^true)
isMyKind: other <Subject> ^ <Boolean> = (
  ^other isKindOfRestoreDialogSubject
)
public createPresenter = (^RestoreDialogPresenter onSubject: self)
public loadSaved = (
  window exit.
  setupIDEWith: (loadFrom: #lastSaved usingPlatform: model) using: model
)
public restoreBackup = (
  window exit.
  setupIDEWith: (loadFrom: #backup usingPlatform: model) using: model
)
public useCurrent = (
  window exit.
  setupIDEWith: List new using: model
)
public title ^ <String> = (
  ^'Newspeak IDE'
)
) : (
)
class RestoreDialogPresenter onSubject: s = Presenter onSubject: s () (
public isKindOfRestoreDialogPresenter ^ <Boolean> = (^true)
isMyKind: other <Fragment> ^ <Boolean> = (
  ^other isKindOfRestoreDialogPresenter
)
definition = (
     ^column: {
      label: 'You have backup changes that are newer than your last save. Do you want to restore these changes, or load from the last save?'.
      row: {
        button: 'Restore from backup' action: [subject restoreBackup].
        button: 'Load older saved version' action: [subject loadSaved].	
        button: 'Use current version, ignoring both saved and backup versions' action: [subject useCurrent]
      }
    }
  )
) : (
)
) : (
)
class HopscotchWebIDE usingPlatform: p = (
	|
	public Map = p collections Map.
        public images = p hopscotch images(*HopscotchImages usingPlatform: p*). 
	public browsing = Browsing usingPlatform: p ide: self.
    public debugging = Debugging usingPlatform: p ide: self.
    public namespacing = Namespacing usingPlatform: p.
    public workspaceHolder = WorkspaceHolder usingPlatform: p ide: self.
    public theWorkspaceManager = WorkspaceManager usingPlatform: p ide: self.
    public documents = Documents usingPlatform: p ide: self.
    public documentHolder = DocumentHolder usingDocumentClass: documents Document ide: self.
	public minitest = Minitest usingPlatform: p.
	public minitestUI = MinitestUI usingPlatform: p minitest: minitest ide: self.
	public parserLib = CombinatorialParsing usingPlatform: p.
	public grammar = NewspeakGrammar usingPlatform: p parsers: parserLib.
    public colorizer = (NewspeakColorization usingPlatform: p grammar: grammar) NS3BrowserColorizer new.
	public webFiles = WebFiles usingPlatform: p.
	public json = JSON usingPlatform: p.
	public deployment
	public psoupDeploymentRuntime = RuntimeForPrimordialSoup.
	public psoupWithMirrorsDeploymentRuntime = RuntimeWithMirrorsForPrimordialSoup.
    public psoupWithHopscotchDeploymentRuntime = RuntimeForHopscotchForHTML.
    public localStorage = p js localStorage.
	public atomicInstaller = p mirrors installer.
	Date = p js global at: 'Date'.
  version_slot ::= maxStoredVersion.
  |
  populateNamespaceUsingPlatform: p.
  deployment:: DeploymentManager usingPlatform: p ide: self.
) (
public launch: s <Subject> inWindow: w <HopscotchShell class> = (
  browsing launch: s inWindow: w
)
public launch: s <Subject> = (
  browsing launch: s inWindow: browsing IDEWindow
)
backup ^ <Map[Symbol, String]> = (
  ^[json decode: (localStorage getItem: #backup)] on: Error do: [:e | Map new].
)
topMostBuilderOf: b <ClassDeclarationBuilder> ^ <ClassDeclarationBuilder> = (
  | outermost <ClassDeclarationBuilder> ::= b. |
  [nil = outermost enclosingClass] whileFalse:
    [outermost:: outermost enclosingClass].
  ^outermost
)
public incrementedVersion = (
  ^version_slot:: version_slot + 1.
)
public installFromBuilders: bs <Array[ClassDeclarationBuilder]> ^ <List[MixinMirror]> = (
(* A centralized method for installing new code from the IDE *)
  ^installFromBuilders: bs into: namespacing Root.
)
public installFromBuilders: bs <Array[ClassDeclarationBuilder]> into: namespace <Map[Symbol, Object]> ^ <List[MixinMirror]> = (
(* A centralized method for installing new code from the IDE *)
  |
  lastBackup <Map[Symbol, String]> = backup.
  (* Make sure all builders are top level *)
  builders <Array[ClassDeclarationBuilder]> = bs collect: [:b <ClassDeclartionBuilder> | topMostBuilderOf: b].
  (* Install the code *)
  mixins = atomicInstaller install: builders into: namespace.
  |
  (* Backup the changes *)
  builders do: [:b <ClassDeclarationBuilder> | 
    (* Add each changed module to the backup *)
    lastBackup at: b name put: (browsing compilationUnitFromSource: b source)
  ].
  (* Save new backup in local storage *)
  localStorage setItem: #backup to: (json encode: lastBackup).
  (* Timestamp latest backup *)
  localStorage setItem: #lastBackupTime to: incrementedVersion.
  ^mixins
)
lastBackupTime ^ <Integer> = (
  | backupString <String> = [localStorage getItem:  #lastBackupTime] on: Error do: [:e | '0']. |
  ^Integer parse: (backupString isNil ifFalse: [backupString] ifTrue: ['0']) radix: 10.
)
lastSavedTime ^ <Integer> = (
  | savedString <String> = [localStorage getItem:  #lastSavedTime] on: Error do: [:e | '0']. |
  ^Integer parse: (savedString isNil ifFalse: [savedString] ifTrue: ['0']) radix: 10.
)
maxStoredVersion ^ <Integer> = (
  ^lastSavedTime max: lastBackupTime
)
public standardPreludeIconNames ^ <List[String]> = (
  ^standardIconNames
)
public standardPreludeMessages ^ <List[String]> = (
  ^standardNames
)
populateIconNamespace: ns <Namespace> ^ <Namespace> = (
	^ns at: 'accept16px' put:  images accept16px;
            at: 'ampleforthDocument' put:  images ampleforthDocument;
	    at: 'cancel16px' put: images cancel16px;
            at: 'clearImage' put: images clearImage;
	    at: 'disclosureClosedImage' put: images disclosureClosedImage;
	    at: 'disclosureOpenImage' put: images disclosureOpenImage;
            at: 'downloadImage' put: images downloadImage;
            at: 'helpImage' put: images helpImage;
            at: 'publicAccessImage' put: images publicAccessImage;
            at: 'protectedAccessImage' put: images protectedAccessImage;
            at: 'privateAccessImage' put: images privateAccessImage;
            at: 'addImage' put: images addImage;
            at: 'backImage' put: images backImage;
            at: 'dropDownImage' put: images dropDownImage;
            at: 'expandImage' put: images expandImage;
            at: 'collapseImage' put: images collapseImage;
            at: 'classPresenterImage' put: images classPresenterImage;
            at: 'classUnknownImage' put: images classUnknownImage;
            at: 'forwardImage' put: images forwardImage;
            at: 'classImage' put: images classImage;
            at: 'itemReferencesImage' put: images itemReferencesImage;
            at: 'historyImage' put: images historyImage;		
            at: 'homeImage' put: images homeImage;
            at: 'newImage' put: images newImage;		
            at: 'refreshImage' put: images refreshImage;
            at: 'findImage' put: images findImage;
            at: 'peekingeye1610' put: images peekingeye1610;
            at: 'saveImage' put: images saveImage;
            at: 'sectionImage' put: images sectionImage;
            yourself.
)
public setupNames = (
  (* Record standard namespace names *)
  standardNames:: namespacing Root keys.
  (* Record standard icon names *)
  standardIconNames:: (namespacing Root at: #Icons) keys.
)
populateNamespaceUsingPlatform: p <Platform> ^ <Namespace> = (
	|
	platformMirror <ObjectMirror> = p mirrors ObjectMirror reflecting: p.
	runtimeClass <ClassMirror> = topLevelClassOf: platformMirror.
	namespace <Namespace> = namespacing Root.
	|
	namespace
		at: 'Icons' put: (populateIconNamespace: namespacing freshNamespace);
		at: runtimeClass mixin name put: runtimeClass reflectee;
		at: 'Browsing' put: Browsing;
		at: 'Minitest' put: Minitest;
		at: 'MinitestUI' put: MinitestUI;
		at: 'Namespacing' put: Namespacing;
		at: 'NewspeakColorization' put: NewspeakColorization;
		at: 'HopscotchWebIDE' put: (topLevelClassOf: (p mirrors ObjectMirror reflecting: self)) reflectee;
		at: 'CombinatorialParsing' put: CombinatorialParsing;
		at: 'NewspeakGrammar' put: NewspeakGrammar;
		at: 'NewspeakASTs' put: NewspeakASTs;
		at: 'NewspeakParsing' put: NewspeakParsing;
        at: 'MetadataParsing' put: MetadataParsing;
        at: 'Debugging' put: Debugging;
        at: 'Documents' put: Documents;
        at: 'DocumentHolder' put: DocumentHolder;        
		at: 'WorkspaceManager' put: WorkspaceManager;
		at: 'WorkspaceHolder' put: WorkspaceHolder;		
		at: 'DeploymentManager' put: DeploymentManager;		
		at: 'RuntimeForPrimordialSoup' put: RuntimeForPrimordialSoup;
		at: 'RuntimeWithMirrorsForPrimordialSoup' put: RuntimeWithMirrorsForPrimordialSoup;
        at: 'RuntimeForHopscotchForHTML' put: RuntimeForHopscotchForHTML;
		at: 'RuntimeForJS' put: RuntimeForJS;
		at: 'RuntimeForJSWithMirrorBuilders' put: RuntimeForJSWithMirrorBuilders;
		at: 'NewspeakCompilation' put: NewspeakCompilation;
        at: 'JavascriptGeneration' put: JavascriptGeneration;
        at: 'JSON' put: JSON;        
        at: 'Newspeak2JSCompilation' put: Newspeak2JSCompilation;
        at: 'KernelForJS' put: KernelForJS;
        at: 'ActorsForJS' put: ActorsForJS;	
        at: 'AliensForJS' put: AliensForJS;
        at: 'MirrorsForJS' put: MirrorsForJS;
        at: 'MirrorGroups' put: MirrorGroups;	
        at: 'Collections' put: Collections;
        at: 'Streams' put: Streams;
        at: 'WebCompiler' put: WebCompiler;
	    at: 'WebFiles' put: WebFiles.
		
	testModules do: [:testModule | namespace at: testModule name put: testModule].

	augmentNamespace: namespace withPlatform: p.

	^namespace
)
) : (
)
topLevelClassOf: om <ObjectMirror> ^ <ClassMirror> = (
	| klass <ClassMirror> ::= om getClass. |
	[klass enclosingObject reflectee isNil] whileFalse: [klass:: klass enclosingObject getClass].
	^klass
)
public augmentNamespace: namespace withPlatform: p = (
	|
	platformMirror <ObjectMirror> = p mirrors ObjectMirror reflecting: p.
	platformClass <ClassMirror> = platformMirror getClass.
	runtimeMirror<ObjectMirror> = platformClass enclosingObject.
	runtimeClass <ClassMirror> = topLevelClassOf: platformMirror.
	|

	runtimeClass slots do:
		[:s <SlotMirror> | | klass <ClassMirror> o <Object> |
		o:: (runtimeMirror getSlot: s name) reflectee.
		o isKindOfBehavior ifTrue:
			[namespace at: o name put: o]].
)
loadFrom: lsKey <Symbol> usingPlatform: p ^ <List[MixinMirror]> = (
   [ | 
     json = JSON usingPlatform: p.
     localStorage = p js localStorage.
     recoveredMap <Map[String]| Nil> =  json decode: ([localStorage getItem:  lsKey] on: Error do: [:e | nil]). 
     recoveredCode = recoveredMap isNil ifFalse: [recoveredMap values] ifTrue: [p collections List new].
     bs <List[ClassDeclarationBuilder]> = recoveredCode collect: [:s <String> |
       p mirrors ClassDeclarationBuilder fromUnitSource: s
    ].
    forbidden <Set[Symbol]> = p collections Set withAll: {#HopscotchWebIDE. #KernelForPrimordialSoup}.    
    filtered = bs reject: [:b | forbidden includes:  b name].
    mixins <List[MixinMirror]> = p mirrors installer install: filtered into: (namespaceGivenPlatform: p).
    |
    ^mixins
  ] on: Error do: [:msg | msg out. nil].
  ^p collections List new
)
public ideUsingPlatform: p = (
   (* The call to ensureLocalStorage: is usually redundant, except when other tools embed the IDE using
       this method as the entry point. *)
        ensureLocalStorage: p js localStorage.
	^HopscotchWebIDE usingPlatform: p
)
setupIDEWith: mixins using: platform = (
    | ide =  ideUsingPlatform: platform.  namespace = ide namespacing Root. |
    augmentNamespace: namespace withPlatform: platform local.
    ide setupNames.
    mixins do: [:m <MixinMirror> | namespace at: m name put: m declaration applyToObject reflectee].
    ide launch: (ide browsing HomeSubject new)
)
loadOrRestoreUsingPlatform: p  = (
  |
  localStorage<Alien[LocalStorage]> = p js localStorage.
  lastBackupTime = Integer parse: ([localStorage getItem:  #lastBackupTime] on: Error do: [:e | 0]) radix: 10.
  lastSavedTime = Integer parse: ([localStorage getItem:  #lastSavedTime] on: Error do: [:e | 0]) radix: 10.
  |

  lastBackupTime > lastSavedTime
    ifTrue: [RestoreDialog usingPlatform: p]
    ifFalse: [setupIDEWith: (loadFrom: #lastSaved usingPlatform: p) using: p] 
)
public main: platform <Platform> args: args <Tuple[String]> = (
	| ide |
    ensureLocalStorage:: platform js localStorage.
    (platform js global at: 'document') at: 'title' put: 'Newspeak IDE'.
    loadOrRestoreUsingPlatform: platform 
)
ensureLocalStorage: localStorage = (
   (localStorage getItem:  #lastBackupTime) isNil ifTrue: [
       localStorage setItem:  #lastBackupTime to: 0.   
   ].
   (localStorage getItem:  #backup) isNil ifTrue: [
       localStorage setItem:  #backup to: '{}'.
       ].           
   (localStorage getItem:  #lastSavedTime) isNil ifTrue: [ 
       localStorage setItem:  #lastSavedTime to: 0.
       ].           
   (localStorage getItem:  #lastSaved) isNil ifTrue: [ 
       localStorage setItem:  #lastSaved to: '{}'.
       ].
)
namespaceGivenPlatform: p ^ <Map[Symbol, Object]> = (
  |
  platformMirror <ObjectMirror> = p mirrors ObjectMirror reflecting: p.
  runtimeClass <ClassMirror> = topLevelClassOf: platformMirror.
  ns = p collections Map new. 
  |
  ns   at: runtimeClass mixin name put: runtimeClass reflectee;
		at: 'Browsing' put: Browsing;
		at: 'Minitest' put: Minitest;
		at: 'MinitestUI' put: MinitestUI;
		at: 'Namespacing' put: Namespacing;
		at: 'NewspeakColorization' put: NewspeakColorization;
		at: 'CombinatorialParsing' put: CombinatorialParsing;
		at: 'NewspeakGrammar' put: NewspeakGrammar;
		at: 'NewspeakASTs' put: NewspeakASTs;
		at: 'NewspeakParsing' put: NewspeakParsing;
        at: 'MetadataParsing' put: MetadataParsing;
		at: 'Debugging' put: Debugging;
        at: 'Documents' put: Documents;
        at: 'DocumentHolder' put: DocumentHolder;    
		at: 'WorkspaceManager' put: WorkspaceManager;
        at: 'WorkspaceHolder' put: WorkspaceHolder;		
		at: 'DeploymentManager' put: DeploymentManager;		
		at: 'RuntimeForPrimordialSoup' put: RuntimeForPrimordialSoup;
		at: 'RuntimeWithMirrorsForPrimordialSoup' put: RuntimeWithMirrorsForPrimordialSoup;
        at: 'RuntimeForHopscotchForHTML' put: RuntimeForHopscotchForHTML;	
		at: 'RuntimeForJS' put: RuntimeForJS;
		at: 'RuntimeForJSWithMirrorBuilders' put: RuntimeForJSWithMirrorBuilders;
		at: 'NewspeakCompilation' put: NewspeakCompilation;
        at: 'JavascriptGeneration' put: JavascriptGeneration;
        at: 'JSON' put: JSON;                
        at: 'Newspeak2JSCompilation' put: Newspeak2JSCompilation;
        at: 'KernelForJS' put: KernelForJS;
        at: 'ActorsForJS' put: ActorsForJS;	
        at: 'AliensForJS' put: AliensForJS;
        at: 'MirrorsForJS' put: MirrorsForJS;
        at: 'MirrorGroups' put: MirrorGroups;	
        at: 'Collections' put: Collections;
        at: 'Streams' put: Streams;
        at: 'WebCompiler' put: WebCompiler;
	    at: 'WebFiles' put: WebFiles.
  testModules do: [:testModule | ns at: testModule name put: testModule].
  augmentNamespace: ns withPlatform: p.
  ^ns
)
) : (
)
