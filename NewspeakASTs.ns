Newspeak3
'Root'
class NewspeakASTs usingPlatform: platform = (
(* Abstract syntax tree node classes for Newspeak.

Issues:
The abbreviations in class and slot names should be replaced with proper identifiers.
Possibly ASTs for self/super/outer/implicitReceiver should be defined here.
The factory selector should be changed to #usingPlatform:.

Copyright 2008 Cadence Design Systems, Inc.
Copyright 2010 Felix Geller and Ryan Macnak

Licensed under the Apache License, Version 2.0 (the ''License''); you may not use this file except in compliance with the License.  You may obtain a copy of the License at  http://www.apache.org/licenses/LICENSE-2.0 *)
|
	private List = platform collections List.
|) (
public class AST = (
(* An abstract syntax tree. An AST knows where in the source code the corresponding construct is. The slots start and end are indices into the source code marking that position. *)
|
public start <Integer> ::= 0. (* Note that 0 is out-of-bounds in a Newspeak string. *)
public end <Integer> ::= 0.
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^self subclassResponsibility
)
assert: block = (
	self assert: block message: 'Assert failed'
)
public copyPositionFrom: other <AST> = (
	self start: other start.
	self end: other end.
)
public isKindOfAST ^<Boolean> = (
	^true
)
) : (
)
public class ASTTool = () (
public blockNode: aNode <BlockAST> = (
	self subclassResponsibility
)
public cascadedSendNode: aNode <CascadedSendAST> = (
	self subclassResponsibility
)
public characterNode: aNode <CharacterAST> = (
	self subclassResponsibility
)
public classNode: aNode <ClassAST> = (
	self subclassResponsibility
)
public codeBodyNode: aNode <CodeBodyAST> = (
	self subclassResponsibility
)
public messageNode: aNode <MessageAST> = (
	self subclassResponsibility
)
public messagePatternNode: aNode <MessagePatternAST> = (
	self subclassResponsibility
)
public methodNode: aNode <MethodAST> = (
	self subclassResponsibility
)
public mixinApplication: node <MixinApplicationAST> = (
	self subclassResponsibility
)
public mixinApplicationClass: node <MixinApplicationClassAST> = (
	self subclassResponsibility
)
public numberNode: aNode <NumberAST> = (
	self subclassResponsibility
)
public returnStatNode: aNode <ReturnStatAST> = (
	self subclassResponsibility
)
public setterSendNode: aNode <SetterSendAST> = (
	self subclassResponsibility
)
public sideNode: aNode <SideAST> = (
	self subclassResponsibility
)
public stringNode: aNode <StringAST> = (
	self subclassResponsibility
)
public symbolNode: aNode <SymbolAST> = (
	self subclassResponsibility
)
public tupleNode: aNode <TupleAST> = (
	self subclassResponsibility
)
public unresolvedSendNode: aNode <UnresolvedSendAST> = (
	self subclassResponsibility
)
public varDeclNode: aNode <VarDeclAST> = (
	self subclassResponsibility
)
public variableNode: aNode <VariableAST> = (
	(* can only be a pseudo variable : self or _here *)
	self subclassResponsibility
)
) : (
)
public class BinaryTypeOpAST = AST (|
	public leftOperand <TypeAST>
	public rightOperand <TypeAST>
	public operator <Symbol>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool binaryTypeOpNode: self
)
) : (
)
public class BlockAST = AST (|
	public body <CodeBodyAST>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool blockNode: self
)
public isKindOfBlockNode ^<Boolean> = ( ^true )
public parameters ^ <List[ParameterAST]> = (
	^body parameters
)
) : (
)
public class BlockTypeAST formals: f return: r = AST (|
	public formals = f.
	public return = r.
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool blockTypeNode: self
)
) : (
)
public class CascadedSendAST = SendAST (|
	public previousSend <SendAST>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool cascadedSendNode: self
)
public end = (
	(* Currently the parser doesn't set start or end positions for
	cascades, so compute from the receiver's elements.  But
	if the parser ever does set the position, take it directly. *)
	^super end = 0
		ifFalse: [super end]
		ifTrue: [message end]
)
public isKindOfCascadedSendNode ^<Boolean> = ( ^true )
public receiver ^ <ExpressionAST> = (
	^self previousSend receiver
)
public start = (
	(* Currently the parser doesn't set start or end positions for
	cascades, so compute from the receiver's elements.  But
	if the parser ever does set the position, take it directly. *)
	^super start = 0
		ifFalse: [super start]
		ifTrue: [previousSend start]
)
public to: aSend <SendAST> cascade: aMessage <MessageAST> = (
	self assert: [aSend isKindOfSendNode].
	self assert: [aMessage isKindOfMessageNode].
	self previousSend: aSend; message: aMessage.
)
) : (
)
public class CharacterAST = LiteralAST () (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool characterNode: self
)
) : (
)
public class ClassBodyAST initializer: init <InitializerAST> instanceSide: iSide <SideAST> classSide: cSide <SideAST> = AST (
(* AST for a class body. *)
|
	public instanceInitializer <InitializerAST> = init.
	public instanceSide <SideAST> = iSide.
	public classSide <SideAST> = cSide.
|start:: instanceInitializer start.
end:: classSide end max: instanceSide end) (
) : (
)
public class ClassDeclarationAST = AST (|
	public header <ClassHeaderAST>
	public instanceSide <SideAST>
	public classSide <SideAST>
|) (
public accessModifier ^<Symbol> = (
	^header accessModifier
)
public apply: tool <ASTTool[T]> ^<T> = (
	^tool classNode: self
)
public category ^ <CategoryAST> = (
	^header category
)
public category: cat <CategoryAST> = (
	header category: cat
)
public classComment ^ <String> = (
	^header classComment
)
public isKindOfClassDeclarationNode ^<Boolean> = (
	^true
)
public name ^ <Symbol> = (
	^header name
)
public selector ^<Symbol> = (
	^header name
)
public superclassName ^ <Symbol> = (
	^header superclassName
)
) : (
public makeMixinNamed: klassName <Token> factoryMsg: cons <MessagePatternAST> inheriting: iList <List[SendAST]> body: body <ClassBodyAST>
 ^ <ClassDeclarationAST> = (
(* adaptor factory method for transition. Using this method, we can create an AST compatible with the unmodified compiler based on the revised parser. *)
	|
	superCall <SendAST> = iList first.
	instanceInitializer <InitializerAST> = body instanceInitializer.
	header = ClassHeaderAST new
				name: klassName value asSymbol;
				constructor: cons;
				superclassName: superCall receiver message selector;
				superclassCall: superCall receiver;
				superConstructorCall: superCall message;
				slots: instanceInitializer slots;
				isSeq: instanceInitializer isSeq;
				initExprs: instanceInitializer initializationExpressions;
				inheritanceChain: iList;
				classComment: body instanceInitializer classComment;
				start: klassName start; end: instanceInitializer end.
	|
	^new
		header: header;
		instanceSide: body instanceSide;
		classSide: body classSide
)
public named: klassName <Token> factoryMsg: cons <MessagePatternAST> inheriting: iList <List[SendAST]> body: body <ClassBodyAST>
 ^ <ClassDeclarationAST> = (
	^body isNil
		ifTrue: [MixinApplicationClassAST named: klassName value factoryMsg: cons inheriting: iList]
		ifFalse: [makeMixinNamed: klassName factoryMsg: cons inheriting: iList body: body]
)
)
public class ClassHeaderAST = AST (|
	public accessModifier
	public name <Symbol>
	public constructor <MessagePatternAST>
	public superclassName <Symbol>
	public superclassCall <NormalSendAST>
	public superConstructorCall <MessageAST>
	public slots <List[SlotDefAST]>
	public isSeq <Boolean>
	public classComment <String>
	public category <CategoryAST>
	public initExprs <List[ExpressionAST]>
	public inheritanceChain <List[SendAST]>
 |) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool classHeaderNode: self
)
public isKindOfClassHeaderAST = (
	^true
)
public superCall ^ <SendAST> = (
	^inheritanceChain first
)
) : (
)
public class ClassPrefixAST name: n
	constructor: k
	superclassName: sn
	superclassCall: scc
	superConstructorCall: sk = AST (
(* A class prefix defines everything about a class except for its mixin.
It is intended to serve as a common component between ordinary class declarations and mixin applications.

For Squeak compatibility, we maintain a category as well (ugh).
*)
|
	public name <Symbol> = n.
	public constructor <MessagePatternAST> = k.
	public superclassName <Symbol> = sn.
	public superclassCall <NormalSendAST> = scc.
	public superConstructorCall <MessageAST> = sk.
	public category <CategoryAST>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool classPrefix: self
)
) : (
)
public class CodeBodyAST = AST (|
	public parameters <List[ParameterAST]>
	public temporaries <List[TemporaryAST]>
	public statements <List[StatementAST]>
	public isSeq <Boolean> ::=true.
	public literalMessages <List[MessageAST]>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool codeBodyNode: self
)
public isKindOfCodeBodyNode ^<Boolean> = (^true)
public parameters: pars <List[ParameterAST]> temporaries: temps <List[TemporaryAST]> statements: stats <List[StatementAST]> = (
	self parameters: pars; temporaries: temps; statements: stats
)
public temporaries: temps <List[TemporaryAST]> statements: stats <List[StatementAST]> = (
	self parameters: List new; temporaries: temps; statements: stats
)
) : (
)
public class ImmutableSlotDefAST = SlotDefAST () (
public isMutable ^<Boolean> = (
	^false
)
) : (
)
public class InitializerAST slots: s initializerExpressions: es comment: c slotsAreSequential: seq = AST (
(* AST for an instance initializer clause. *)
|
	public slots = s.
	public initializationExpressions <List[ExpressionAST]> = es.
	public classComment = c.
	public isSeq <Boolean> = seq.
|) (
) : (
)
public class KeywordPatternAST = PatternAST (|
	public keywords <TupleAST>
	public patterns <TupleAST>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool keywordPatternNode: self
)
) : (
)
public class KeywordPatternPairAST = AST (|
	keyword <Symbol>
	pattern
|) (
) : (
)
public class LiteralAST = AST (|
	public value <Object>
|) (
public isKindOfLiteralNode ^<Boolean> = (^true)
public printOn: stream = (
	stream nextPutAll: class simpleName.
	stream nextPutAll: ': '.
	value printOn: stream.
)
) : (
)
public class LiteralPatternAST = PatternAST (|
	public literal <LiteralAST>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool literalPatternNode: self
)
) : (
)
public class MessageAST = AST (|
	public selector <Symbol>
	public arguments <List[ExpressionAST]>
	public isEventual <Boolean> ::= false.
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool messageNode: self
)
public isKindOfMessageNode ^<Boolean> = (^true)
public send: sel <Symbol> with: args <List[ExpressionAST]> = (
	self selector: sel; arguments: args
)
) : (
)
public class MessagePatternAST = AST (
|
	public accessModifier <Symbol>
	public selector <Symbol>
	public parameters <List[ParameterAST]>
	public returnType <TypeAST>
	public typePattern <TypePatternAST>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool messagePatternNode: self
)
public selector: s <Symbol> parameters: p <List[ParameterAST]> = (
  self selector: s; parameters: p
)
public selector: s <Symbol> parameters: p <List[ParameterAST]> returnType: t <TypeAST> = (
  self selector: s; parameters: p; returnType: t
)
public selector: s <Symbol> parameters: p <List[ParameterAST]> returnType: t <TypeAST> typePattern: tp <TypePatternAST> = (
  self selector: s; parameters: p; returnType: t; typePattern: tp
)
public selector: s <Symbol> returnType: t <TypeAST> = (
  self selector: s; returnType: t
)
) : (
)
public class MethodAST = AST (|
	public pattern <MessagePatternAST>
	public body <CodeBodyAST>
|) (
public accessModifier ^ <Symbol> = (
	^pattern accessModifier
)
public accessModifier: am <Symbol> = (
	pattern accessModifier: am
)
public apply: tool <ASTTool[T]> ^<T> = (
	^tool methodNode: self
)
public pattern: p <MessagePatternAST> body: b <CodeBodyAST> accessModifier: am <Symbol> = (
	pattern: p.
	body: b.
	accessModifier: am.
)
public printOn: stream = (
	stream nextPutAll: 'MethodAST: '.
	stream nextPutAll: selector
)
public selector ^<Symbol> = (
	^pattern selector
)
) : (
)
public class MixinApplicationAST = AST (
(* Represents a literal mixin application. *)
|
	public mixinExpression <NormalSendAST>
	public superclassExpression <NormalSendAST> (* should eventually go away *)
	public classPrefix <ClassPrefixAST>
 |) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool mixinApplication: self
)
) : (
public prefix: cp mixin: me = (
(* ultimately should be a primary factory *)
	^new classPrefix: cp; mixinExpr: me; superclassExpr: cp superclassCall; yourself
)
)
public class MixinApplicationClassAST named: klassName factoryMsg: cons inheriting: iList = AST (
(* This class represents a class defined via a mixin application - that is, a class that does not provide a class body. *)
|
	public accessModifier <Symbol>
	public name <Symbol> = klassName asSymbol.
	public constructor <MessagePatternAST> = cons.
	public inheritanceChain <List[SendAST]> = iList.
	public category <CategoryAST>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool mixinApplicationClass: self
)
public isKindOfMixinApplicationClassAST ^<Boolean> = (
	^true
)
) : (
)
public class MutableSlotDefAST = SlotDefAST () (
public isMutable ^ <Boolean> = (^true)
) : (
)
public class NumberAST = LiteralAST () (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool numberNode: self
)
public isKindOfNumberNode ^<Boolean> = ( ^true )
) : (
)
public class ParameterizedTypeAST = AST (|
	public generic <TypeIdAST>
	public arguments <List[TypeAST]>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool genericInvocationNode: self
)
) : (
)
public class PatternAST = LiteralAST (
) (
) : (
)
public class ReturnStatAST = AST (|
	public expression <ExpressionAST>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool returnStatNode: self
)
public isKindOfReturnStatNode ^<Boolean> = ( ^true )
) : (
)
public class SendAST send: aMsg = AST (|
	public message <MessageAST> ::= aMsg.
|) (
public isKindOfSendNode ^<Boolean> = ( ^true )
public receiver = (
	^self subclassResponsibility
)
) : (
)
public class SetterSendAST to: r <ExpressionAST> send: m <MessageAST> start: s <integer> end: e <Integer> = SendAST send: m (|
	public receiver <ExpressionAST> ::= r.
|
	start: s.
	end: e.
) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool setterSendNode: self
)
public printOn: stream = (
	stream nextPutAll: 'SetterSendAST: '.
	stream nextPutAll: message selector.
)
) : (
)
public class SideAST = AST (
(* An AST for one side of a class *)
|
	public nestedClasses <Collection[ClassDeclarationAST]>
	public methods <Collection[MethodAST]>
	public lazySlots <List[SlotDefAST]>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool sideNode: self
)
) : (
)
public class SlotDefAST = AST (|
	public slotDecl <VariableAST>
	public initializer <ExpressionAST>
	public accessModifier
	public isLazy ::= false.
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool slotDefNode: self
)
public isMutable ^ <Boolean> = (self subclassResponsibility)
public name ^ <Symbol> = (
	^slotDecl name
)
public type ^ <TypeAST> = (
	^slotDecl type
)
) : (
)
public class StringAST = LiteralAST () (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool stringNode: self
)
) : (
)
public class SymbolAST = LiteralAST () (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool symbolNode: self
)
) : (
)
public class TupleAST = AST (|
	public elements <List[ExpressionAST]>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool tupleNode: self
)
public isKindOfTupleNode ^<Boolean> = ( ^true )
) : (
)
public class TypeIdAST = AST (|
	public name <Symbol>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool typeIdNode: self
)
) : (
)
public class UnaryTypeOpAST = AST (| public operand <TypeAST> public operator <Symbol> |) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool unaryTypeOpNode: self
)
) : (
)
public class UnresolvedSendAST = SendAST (|
	public receiver <ExpressionAST>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool unresolvedSendNode: self
)
public isKindOfUnresolvedSendNode ^<Boolean> = (
	^true
)
public printOn: stream = (
	stream nextPutAll: 'NormalSendAST: '.
	stream nextPutAll: message selector.
)
public to: anExpression <ExpressionAST> send: aMessage <MessageAST> = (
	self receiver: anExpression; message: aMessage
)
public to: anExpression <ExpressionAST> send: selector with: arguments = (
	to: anExpression send: (MessageAST new send: selector with: arguments)
)
) : (
)
public class VarDeclAST name: aName type: aType = AST (|
	public name <Symbol> = aName.
	public type <TypeAST> = aType.
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool varDeclNode: self
)
) : (
)
public class VariableAST = AST (|
	public name <Symbol>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool variableNode: self.
)
public isKindOfVariableNode ^<Boolean> = (^true)
public printOn: stream = (
	stream nextPutAll: class simpleName.
	stream nextPutAll: ': '.
	stream nextPutAll: name.
)
) : (
)
public class VariablePatternAST = PatternAST (|
	variableName <Symbol>
|) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool variablePatternNode: self
)
) : (
public variableName: vn start: s end: e = (
	^self new variableName: vn; start: s; end: e; yourself
)
)
public class WildcardPatternAST = PatternAST (
) (
public apply: tool <ASTTool[T]> ^<T> = (
	^tool wildcardPatternNode: self
)
) : (
public start: s end: e = (
	^self new start: s; end: e; yourself
)
)
) : (
)
