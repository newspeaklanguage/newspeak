<div class = "ampleforthDocumentClass" name = "TelescreenTemplate" classBody = "(  
  |
  private List =  platform collections List.
  private Map = platform collections Map.
  private Subject = platform hopscotch Subject.
  private ObjectMirror = platform mirrors ObjectMirror.
  private ProgrammingPresenter = ide browsing ProgrammingPresenter.
  private Root = ide namespacing Root.
  private NamespaceSubject = ide browsing NamespaceSubject.
  private Utilities = ide browsing Utilities.
  private webFiles = ide webFiles.
  private Documents <Documents> = ide documents.
  private documentHolder <DocumentHolder class> = ide documentHolder.
  private Document = Documents Document.
  private DocumentSubject <DocumentSubject class> = ide documents DocumentSubject.
  presentation <Presentation> = Presentation named: name.
  |
) (
class TelescreenPresenter onSubject: s <TelescreenSubject> (*:exemplar: TelescreenPresenter onSubject: (TelescreenSubject onModel: (Presentation named: 'ExemplarPresentation'))*) = ProgrammingPresenter onSubject: s (
  | public activeSlide <DocumentPresenter>  |
) (
public isKindOfTelescreenPresenter ^ <Boolean> = (
  ^true
)
isMyKind: other <Fragment>  ^ <Boolean> = (
  ^other isKindOfTelescreenPresenter
)
slideNumbers ^ <List[Integer]> = (
  | sns = List new. |
  1 to: subject numberOfSlides do: [:i <Integer> | sns add: i].
  ^sns
)
currentSlide ^ <DocumentPresenter> = (
  ^subject currentSlide presenter
)
newSlideButton ^ <ButtonFragment> = (
  ^addButtonWithAction: [updateGUI: [subject new]]
)
nextSlideButton ^ <ButtonFragment> = (
  ^button: 'Next' action: [updateGUI: [subject next]]
)
previousSlideButton ^ <ButtonFragment> = (
  ^button: 'Previous' action: [updateGUI: [subject previous]]
)
respondToRemoveCurrentSlide = (
  updateGUI: [subject removeCurrentSlide]
)
respondToDelete = (
	updateGUI: [
      presentationListSubject removePresentation: subject presentation.
	  enterSubject: presentationListSubject
      ]
)
sideBarVisibilityButton ^ <ButtonFragment> = (
  | sideBarDisplayLabel <String> = subject displaySideBar ifTrue: ['Hide Slide List'] ifFalse: ['Display Slide List ']. |
  ^button: sideBarDisplayLabel action: [updateGUI: [subject displaySideBar: subject displaySideBar not]]
)
sideBar = (
   subject displaySideBar ifFalse: [^nothing].
  ^column: (slideNumbers collect: [:si <Integer> |  
      link: (subject nameOfSlide: si) action: [updateGUI: [subject slideNumber: si]]])
)
respondToDuplicateCurrentSlide = (
  updateGUI: [subject duplicateCurrentSlide]
)
upgradeButton ^ <ButtonFragment> = (
  ^button: 'Upgrade' action: [updateGUI: [subject fixImagesInCurrentSlide]]
)
tsMenu ^ <Menu> = (
  ^menuWithLabelsAndActions: {
        {'Duplicate Current Slide'. [respondToDuplicateCurrentSlide]}.
        {'Remove Current Slide'. [respondToRemoveCurrentSlide]}
        {'Delete'. [respondToDelete]}.
        {'Inspect Presenter'. [respondToInspectPresenter]}.
	}
)
helpText ^ <Fragment> = (
  | 
  mapping = Map new. 
  menuImage = Utilities uriForIconNamed: #hsDropdownImage. 
  referenceImage = Utilities uriForIconNamed: #itemReferencesImage.
  |
  mapping
    at: #newSlideButton put: newSlideButton;
    at: #previousSlideButton put: previousSlideButton;
    at: #nextSlideButton put: nextSlideButton;
    at: #hopscotchTelescreenActionsMenuButton put: (dropDownMenu: [tsMenu]);
    at: #hopscotchTelescreenReferencesButton put: (itemReferencesButtonWithAction: [browseSelector: subject name]);
    at: #hopscotchSaveButton put: (saveButtonWithAction: [subject save]);
    at: #hopscotchHelpButton put: helpButton;
    at: #hopscotchRefreshButton put: refreshButton;
    at: #hopscotchSaveButton put: (saveButtonWithAction: [subject save]).

  ^ampleforth: 'A TelescreenPresenter provides a view of a slide presentation. The first line tells you the presentation name and slide number, and provides the following buttons:
  <ul>
  <li> <div class=&quot;newSlideButton&quot;> </div>Creates a new blank slide immediately after the current slide, and makes it the current slide. </li>
  <li> <div class=&quot;previousSlideButton&quot;> </div>Moves to the preceding slide, if it exists. </li>
  <li> <div class=&quot;nextSlideButton&quot;> </div>Moves to the next slide, if it exists. </li>
  <li> <div class=&quot;hopscotchTelescreenReferencesButton&quot;> </div>Allows you to find references to this presentation. </li>
  <li><div class=&quot;hopscotchSaveButton&quot;> </div> Downloads the presentation. The presentation is saved as a Newspeak Document, ergo a zipped directory with the same name as the presentation, inside of which each slide is saved as an individual Newspeak Document.</li>
 <li><div class=&quot;hopscotchRefreshButton&quot;> </div> Refreshes the display</li>
    <li><div class=&quot;hopscotchHelpButton&quot;> </div> Shows this help message.</li>
   <li> <div class=&quot;hopscotchTelescreenActionsMenuButton&quot;> </div> Opens a menu of additional operations, such as removing or duplicating the current slide, or inspecting this presenter.
 </ul> 
The current slide is shown below all of these controls. Each slide is itself a nested document, with the usual Document affordances. See it''s help section for details.
'  mapping: mapping
)
updateStateFrom: oldPresenter <Self> = (
  subject slideNumber: oldPresenter subject slideNumber.
  subject displaySideBar: oldPresenter subject displaySideBar.
  subject alreadyPresented: oldPresenter subject alreadyPresented.
)
definition ^ <Fragment> = (
  activeSlide:: currentSlide.
  ^column: {
    helpSection.
    row: {
       label: subject name.
       smallBlank.
       label: 'Slide ', subject slideNumber printString.
       smallBlank.       
       newSlideButton.
       smallBlank.
       previousSlideButton.
       smallBlank.  
       nextSlideButton.
       smallBlank.
       sideBarVisibilityButton.
       smallBlank.
       itemReferencesButtonWithAction: [browseSelector: subject name].
       smallBlank.
       refreshButton.			
       smallBlank.
       helpButton.
       smallBlank.
       smallBlank.
       upgradeButton.       
       dropDownMenu: [tsMenu].
      }.
      row: {sideBar. mediumBlank. activeSlide}
    }
)
updateVisualsFromSameKind: oldPresenter <TelescreenPresenter> ^ <Alien[Element]> = (
  (* If the we have switched to a different slide, we must not the update the slide based the one that was displayed before -
      they are unrelated. *)
  subject ~= oldPresenter subject ifTrue: [ 
    (* Use the new visual, disregarding the old presenter entirely. This may need to be refined. *)
     ^replaceVisual: oldPresenter visual with: substance visual
  ].
  updateStateFrom: oldPresenter.
   substanceSlot:: nil.
   ensureSubstance.
   currentSlide subject = oldPresenter activeSlide subject ifFalse: [
     | oldSlide <DocumentPresenter> = subject alreadyPresented at: currentSlide subject model ifAbsent: []. |
     oldSlide isNil ifFalse: [
       currentSlide updateVisualsFrom: oldSlide
       ]
     ].
  ^super updateVisualsFromSameKind: oldPresenter
)
) : (
)
public class TelescreenSubject onModel: m <Presentation> (* :exemplar: TelescreenSubject onModel: (Presentation named: 'ExemplarPresentation') *)= Subject onModel: m (
  (* Represents an actively viewed presentation.  At any time, the presentation is at a particular slide, represented by its index, #currentSlideIndex, in the slides list. The actual document representing the slide is accessible by invoking #currentSlide.
*)
  |
  public name <String> = presentation name.
  currentSlideIndex <Integer> ::= 1 min: presentation slides size.
  slides <List[DocumentSubject]> ::= presentation slides collect: [:s <Document> | DocumentSubject onModel: s].
  public displaySideBar <Boolean> ::= false.
  public alreadyPresented <Map[Document, DocumentPresenter]> ::= Map new.
  |
) (
public createPresenter ^ <TelescreenPresenter> = (
  ^TelescreenPresenter onSubject: self
)
public isKindOfTelescreenSubject ^ <Boolean> = (
  ^true
)
isMyKind: other <Fragment>  ^ <Boolean> = (
  ^other isKindOfTelescreenSubject
)
public presentation ^ <Presentation> = (
  ^model
)
public slideNumber ^ <Integer> = (
  ^currentSlideIndex
)
public currentSlide ^ <DocumentSubject> = (
  slides isEmpty ifTrue: [new].
  ^slides at: currentSlideIndex
)
public numberOfSlides ^ <Integer> = (
  ^slides size
)
public nameOfSlide: i <Integer> ^ <String> (* :exemplar: nameOfSlide: 1 *) = (
  ^name, '_slide_', i printString
)
public slideNumber: i <Integer> ^ <Integer> (* :exemplar1: slideNumber: 1 *) 
(* :exemplar2: slideNumber: 0 *) (* :exemplar3: slideNumber: numberOfSlides + 1 *) 
(* :exemplar4: slideNumber: -1 *)= (
  ^currentSlideIndex:: i
)
maxClassNumber = (
  | classNums = presentation slides collect: [:s <Document> | Integer parse: (s name copyFrom: (s name indexOf: '_slide_') +7 to: s name size)]. |
  ^classNums reduce: [:cn1 :cn2 | cn1 max: cn2] ifEmpty: [0].
)
public title = (
  ^'Telescreen Presentation: ', name
)
public duplicateCurrentSlide = (
  |
  ed <DocumentSubject> = currentSlide cloneNamed: '_slide_', (maxClassNumber + 1) printString.
  l1 <List[DocumentSubject]> = (slides copyFrom: 1 to: currentSlideIndex) addLast: ed; yourself.
  l2 <List[DocumentSubject]> = slides copyFrom: currentSlideIndex +1 to: slides size.
  |
    slides:: l1, l2.
  model slides: (slides collect: [:s <DocumentSubject> | s document]).
  next.
)
public cloneNamed: n <String> ^ <TelescreenSubject> (* :exemplar: cloneNamed: #Dummy *) = (

   | (* create dummy presentation; it's purpose is to easily compute the names for the cloned slides  *)
   nt <TelescreenSubject> = class onModel: (Presentation named: n). 
   mb = Documents documentHolderMixinBuilder.
   contents <List[String]> = List new.
   |
   (* phase 1: clone the document classes for all the slides *)
   1 to: slides size do: [:i <Integer> |
      | 
      s <Document> = presentation slides at: i. 
      slideName = nt nameOfSlide: i.
      |
      (* We could do : s cloneNamed: slideName, but that would do a full atomic install for each slide. This way we do the install just once, at the end *)
      mb nestedClasses addFromSource: 'public class ', slideName, ' contents: c <String> = Document named: #', slideName,  ' contents: c ', s classSource.
      contents add: s contents.
   ].
   (* clone is not complete until we install *)
   mb declaration install.
   (* phase 2: clone the slides *)
   1 to: slides size do: [:i <Integer> |
      | 
      slideName = nt nameOfSlide: i.
      |
      nt presentation slides add: ((((ObjectMirror reflecting: documentHolder ) evaluate: slideName) result reflectee) contents: (contents at: i)).
   ].
   (* create a coherent clone *)
   ^class onModel: nt presentation
)
public fixImages = (
(* temp method to upgrade old slides *)
  slideNumber: 1.
  1 to: numberOfSlides do: [:i |
     currentSlide nameImages.
     next
  ]
)
public fixImagesInCurrentSlide = (
(* temp method to upgrade old slides *)
     #BOGUS yourself.
     currentSlide nameImages.
)
public addContentsUsingFolder: folder <JSZip> = (
  (* add docs to folder *)
  slides do: [:s <DocumentSubject> | folder file: s name, '.zip' contents: s createFolder asBlob].
)
public insertSlide: slide <Document> = (
  |
  ed <DocumentSubject> = DocumentSubject onModel: slide.
  l1 <List[DocumentSubject]> = (slides copyFrom: 1 to: currentSlideIndex) addLast: ed; yourself.
  l2 <List[DocumentSubject]> = slides copyFrom: currentSlideIndex +1 to: slides size.
  |
  slides:: l1, l2.
  model slides: (slides collect: [:s <DocumentSubject> | s document]).
  next.
)
public loadContentsUsingFolder: folder <Alien[JSZip]> = (
| 
namespace = NamespaceSubject onModel: Root key: 'Telescreen root'. 
slideNames <List[String]> = List new.
|
  folder forEach: [:path :zo | slideNames add: (path at: #name)].
  Documents loadTranscluded: folder 
       into: namespace 
       ifSuccess: [
          slideNames do: [:sn <String> |
          | slideName = sn copyFrom: (sn lastIndexOf: '/') + 1 to: (sn size -4). |
             (* extract document from IDE root and add to my slides *)
             insertSlide: (namespace model removeKey: slideName ifAbsent: [('Did not find ', slideName, 'at root') out. Error signal]).
          ].
          slideNames isEmpty ifFalse: [slideNumber: 1].
          nil
       ] 
       ifFailure: [:s <String> | 
          s out. 
          Error signal: 'Failed to load presentation! ', s
       ]
)
public new = (
  insertSlide: (currentMaster cloneButDontInstall: name, '_slide_', (maxClassNumber + 1) printString) 
)
public next = (
  currentSlideIndex < slides size ifTrue: [
    currentSlideIndex > 0 ifTrue: [alreadyPresented at: currentSlide model put: currentSlide presenter].
    currentSlideIndex:: currentSlideIndex + 1
    ]
)
public previous = (
  currentSlideIndex > 1 ifTrue: [
    alreadyPresented at: currentSlide model put: currentSlide presenter.
    currentSlideIndex:: currentSlideIndex - 1
    ]
)
public removeCurrentSlide = (
  |
  l1 <List[DocumentSubject]> = (slides copyFrom: 1 to: currentSlideIndex -1).
  l2 <List[DocumentSubject]> = slides copyFrom: currentSlideIndex +1 to: slides size.
  removedSlide <Document> = currentSlide model.
  |  
  slides:: l1, l2.
  model slides: (slides collect: [:s <DocumentSubject> | s document]).
  previous.
  alreadyPresented removeKey: removedSlide.
)
) : (
)
public class Presentation named: n <String> (* :exemplar: Presentation named: 'ExemplarPresentation' *) = (
  (* The model for a presentation. A presentation has a #name, and consists of a list of Documents, #slides, each representing a slide.*)
  | public name <String> ::= n.  public slides <List[Document]> ::= List new. |
) (
) : (
)
telescreenPresenter ^ <TelescreenPresenter> = (
  ^telescreenSubject presenter
)
public addContentsUsingFolder: contentsFolder = (
  telescreenSubject addContentsUsingFolder: contentsFolder
)
public loadContentsUsingFolder: folder <Alien[JSZip]> = (
   telescreenSubject loadContentsUsingFolder: folder
)
masterSlidePage = (

 (* intended to transclude a page which in turn transcludes a selection of master slides. That would be a document where each transcluded master would be accessible via a public accessor,  so easy to import. *)
)
NewspeakMasterSlide = (
  ^Root at: #NewspeakMasterSlide
)
currentMaster = (
  (* Return the currently selected master slide. For use when creating a new slide *)
  (* for now, hardwired to use the Newspeak master slide *)
  #BOGUS yourself.
  ^NewspeakMasterSlide
)
telescreenSubject ^ <TelescreenPresenter> = (
  ^TelescreenSubject onModel: presentation
)
) : (
)
" </div><div class = "ampleforthDocumentBody"><body><div class="self_ampleforth" contenteditable="true" style="border: 2px solid blue; resize: horizontal; overflow: auto; overflow-wrap: break-word; width: 1127px;" onkeyup="updateRawHTML()" ondragover="((e) => {e.preventDefault();})(event)" ondrop="((e) => {
              e.preventDefault();
            // Calculate the drop position within the text
            const range = document.caretRangeFromPoint(e.clientX, e.clientY);
            // Create a new text node to separate the image from the surrounding text
            const textNode = document.createTextNode('\u00A0'); // Non-breaking space
            // Insert the text node at the calculated position
            range.insertNode(textNode);
            // Loop through dropped files
            for (const file of e.dataTransfer.files) {
                if (file.type.startsWith('image/')) {
                              const imageElement = document.createElement('img');
                              // Set the image element src field to a blob URL we create
                              imageElement.src = window.URL.createObjectURL(file);
                              imageElement.style.maxWidth = '300px';
                              imageElement.name = file.name;
                              // Insert the image after the text node
                              textNode.after(imageElement);               
               } // images
               if (file.type.startsWith('video/')) {
                              const videoElement = document.createElement('video');
                              // Set the image element src field to a blob URL we create
                              videoElement.src = window.URL.createObjectURL(file);
                              videoElement.style.maxWidth = '300px';
                              videoElement.controls = true;
                              videoElement.title = file.name;
                              console.log(file.name);
                              console.log(videoElement.title);
                              // Insert the video after the text node
                              textNode.after(videoElement);    
               } // video
               if (file.type.startsWith('audio/')) {
                              const audioElement = document.createElement('audio');
                              // Set the image element src field to a blob URL we create
                              audioElement.src = window.URL.createObjectURL(file);
                              audioElement.style.maxWidth = '300px';
                              audioElement.controls = true;
                              audioElement.title = file.name;
                              // Insert the audio after the text node
                              textNode.after(audioElement);    
               } // audio
            }//for (const file of e.dataTransfer.files
            updateRawHTML();
  }// ondrop body
  )//ondrop closure
  (event)"> <!-- self_ampleforth -->

  <div><br></div><div>&nbsp;  <div class="ampleforth" documentname="TelescreenTemplate" name="telescreenPresenter" contenteditable="false" ampletid="1" style="display: inline-flex;"></div> </div><div><br></div>    <script>

        const textContainer = document.querySelector('p');
</div></div></script></div></body></div>   <script type="text/javascript">
      function scheduleTurn(timeout) {
        if (timeout >= 0) {
          setTimeout(function() {
            var timeout = Module._handle_message();
            scheduleTurn(timeout);
          }, timeout);
        }
      }

      var Module = {
        noInitialRun: true,
        noExitRuntime: true,
        onRuntimeInitialized: function() {
          var url = new URLSearchParams(window.location.search);
          var request = new XMLHttpRequest();
          request.open("GET", url.get("snapshot"), true);
          request.responseType = "arraybuffer";
          request.onload = function (event) {
            var jsBuffer = new Uint8Array(request.response);
            var cBuffer = _malloc(jsBuffer.length);
            writeArrayToMemory(jsBuffer, cBuffer);
            Module._load_snapshot(cBuffer, jsBuffer.length);
            _free(cBuffer);
            scheduleTurn(0);
          };
          request.send();
        },
        print: function(text) {
          if (arguments.length > 1) {
            text = Array.prototype.slice.call(arguments).join(" ");
          }
          console.log(text);
        },
        printErr: function(text) {
          if (arguments.length > 1) {
            text = Array.prototype.slice.call(arguments).join(" ");
          }
          console.error(text);
        },
        setStatus: function(text) {
          console.log(text);
        },
      };
    </script>
<script async type="text/javascript" src="primordialsoup.js"></script>
    <script src="CodeMirror/lib/codemirror.js"></script>
    <link rel="stylesheet" href="CodeMirror/lib/codemirror.css"></link>
    <script src="CodeMirror/addon/display/autorefresh.js"></script>