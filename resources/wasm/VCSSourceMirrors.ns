Newspeak3
'MemoryHole'
class VCSSourceMirrors usingPlatform: p vcs: vcs ide: ide = (
(* Newspeak Source Mirrors for version control

Copyright (c) 2010-2011 Matthias Kleine

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the ''Software''), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED ''AS IS'', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
*)
||
private Differencer = vcs diffing Differencer.
private imageMirrorCache = vcs imageMirrorCache.

private PackageInfo = p squeak PackageInfo.
private Map = p squeak Dictionary.
private List = p squeak OrderedCollection.
MultiByteBinaryOrTextStream = p squeak MultiByteBinaryOrTextStream.
PNGReadWriter = p squeak PNGReadWriter.
Script = p squeak Script.

private newspeakSourceMirrors = vcs newspeakSourceMirrors.
private smalltalkSourceMirrors = vcs smalltalkSourceMirrors.
private Root = ide namespacing Root.

(* Other fields *)
public mirrorLoader = MirrorLoader new.
||
) (
class ImageSourceMirror = SourceMirror (|
	public name
	public form
|) (
public children = (
	^{}
)
public contents = (
	^ByteArray streamContents: [:stream | streamTo: stream]
)
public contentsEquals: other = (
	form unhibernate.
	other form unhibernate.
	^form extent = other form extent
		and: [form bits = other form bits]
)
public filename = (
	^name, '.png'
)
public installAsToplevelClass = (
	Root at: name asSymbol put: form
)
public isImageMirror = (
	^true
)
public streamTo: stream <WriteStream> = (
	stream binary.
	PNGReadWriter putForm: form onStream: stream
)
) : (
public fromStream: s name: n = (
	^self new name: n; form: (PNGReadWriter formFromStream: s contents asByteArray readStream)
)
)
class MirrorLoader = () (
public imageMirrorsNotCorrespondingTo: mirrors = (
	| classes packages images texts scripts |

	#BOGUS yourself. (* Please refactor me! *)

	classes:: topLevelClasses select: [:each |
		mirrors noneSatisfy: [:mirror |
			mirror isKindOfClassDeclarationMirror and: [mirror name = each name]]].
	classes:: classes collect: [:each |
		NameOnlySourceMirror
			named: each name , ' [module]'
			actualMirror: (Future computing: [imageMirrorCache at: each name , '.ns' ifAbsentPut: [mirrorForClass: each]])].

	packages:: PackageInfo allPackages select: [:each |
		mirrors noneSatisfy: [:mirror | mirror isPackageMirror and: [mirror name = each name]]].
	packages:: packages collect: [:each |
		NameOnlySourceMirror
			named: each name , ' [package]'
			actualMirror: (Future computing: [imageMirrorCache at: each name , '.st' ifAbsentPut: [mirrorForPackage: each]])].

	images:: List new.
	Root keysAndValuesDo: [:name :each |
		(each isForm and: [name ~= #Display] and: [
			mirrors noneSatisfy: [:mirror |
				mirror isImageMirror and: [mirror name = name]]]) ifTrue: [
					images add: (ImageSourceMirror new name: name; form: each)]].
	images:: images collect: [:ea | NameOnlySourceMirror named: ea name , ' [image]' actualMirror: ea].

	texts:: List new.
	Root keysAndValuesDo: [:name :each |
		(each isString and: [mirrors noneSatisfy: [:mirror |
			mirror isTextMirror and: [mirror name = name]]]) ifTrue: [
					texts add: (TextSourceMirror new name: name; contents: each)]].
	texts:: texts collect: [:ea | NameOnlySourceMirror named: ea name , ' [text]' actualMirror: ea].

	scripts:: List new.
	Root keysAndValuesDo: [:name :each |
		(each isKindOfScript and: [mirrors noneSatisfy: [:mirror |
			mirror isScriptMirror and: [mirror name = name]]]) ifTrue: [
					scripts add: (ScriptSourceMirror new name: name; type: each type; content: each content)]].
	scripts:: scripts collect: [:ea | NameOnlySourceMirror named: ea name , ' [script]' actualMirror: ea].

	^classes , packages , images , texts , scripts
)
public installMirrorsAsToplevelClasses: newMirrors = (
	| changedMirrors |

	#BOGUS yourself. (* Why is the argument a dictionary? *)

	changedMirrors:: newMirrors values select: [:each |
		self
			mirrorForClassInImageCorrespondingTo: each
			ifPresent: [:it | (Differencer allEqual: {each. it}) not]
			ifAbsent: [true]].

	(* This installation is not atomic *)
	smalltalkSourceMirrors installMirrors:
		(changedMirrors select: [:ea | ea isPackageMirror]).
	newMirrors do: [:ea |
		ea isImageMirror ifTrue: [Root at: ea name asSymbol put: ea form].
		ea isTextMirror ifTrue: [Root at: ea name asSymbol put: ea contents]].

	(* This installation is atomic *)
	newspeakSourceMirrors installMirrors:
		(changedMirrors select: [:ea | ea isKindOfClassDeclarationMirror]).
)
public methodMirrorForStream: stream ifError: errorBlock = (
	#BOGUS yourself. (* Used by merging UI; consider refactoring. *)

	^newspeakSourceMirrors methodDeclCache mirrorForStream: stream ifError: errorBlock
)
mirrorForClass: aClass = (
	^newspeakSourceMirrors mirrorForClass: aClass
)
public mirrorForClassInImageCorrespondingTo: mirror ifPresent: presentBlock ifAbsent: absentBlock = (
	| correspondingMirror |

	mirror isPackageMirror ifTrue: [
		correspondingMirror:: imageMirrorCache
			at: mirror filename
			ifAbsentPut: [
				|pkgInfo|
				pkgInfo:: PackageInfo allPackages
					detect: [:e | e name = mirror name]
					ifNone: [^absentBlock value].
				mirrorForPackage: pkgInfo].
		^presentBlock value: correspondingMirror.
	].

	mirror isKindOfClassDeclarationMirror ifTrue: [
	correspondingMirror:: imageMirrorCache
	      at: mirror filename
	      ifAbsentPut: [ | existingClass |
	              existingClass:: Root
	                      at: mirror name asSymbol
	                      ifAbsent: [^ absentBlock value].
				existingClass isBehavior ifFalse: [^ absentBlock value].
	              mirrorForClass: existingClass].
	^presentBlock value: correspondingMirror].

	mirror isImageMirror ifTrue: [
		| form = Root at: mirror name asSymbol ifAbsent: [^absentBlock value]. |
		form isForm ifFalse: [^absentBlock value].
		^presentBlock value: (ImageSourceMirror new name: mirror name; form: form)].

	mirror isTextMirror ifTrue: [
		| text = Root at: mirror name asSymbol ifAbsent: [^absentBlock value]. |
		text isString ifFalse: [^absentBlock value].
		^presentBlock value: (TextSourceMirror new name: mirror name; contents: text)].
	
	mirror isScriptMirror ifTrue: [
		| script = Root at: mirror name asSymbol ifAbsent: [^absentBlock value]. |
		script isKindOfScript ifFalse: [^absentBlock value].
		^presentBlock value: (ScriptSourceMirror new name: mirror name; type: script type; content: script content)].

	halt.
)
mirrorForPackage: pkgInfo = (
	^smalltalkSourceMirrors mirrorForPackage: pkgInfo
)
public mirrorForStream: stream name: name = (
	(name endsWith: '.png') ifTrue:
		[^ImageSourceMirror fromStream: stream name: (name allButLast: 4)].

	(name endsWith: '.txt') ifTrue:
		[^TextSourceMirror new
			name: (name allButLast: 4);
			contents: (streamContentsForTextResource: stream)].

	(name endsWith: '.js') ifTrue:
		[^ScriptSourceMirror new
			name: (name allButLast: 3);
			type: 'js';
			content: (streamContentsForTextResource: stream)].

	(name endsWith: '.css') ifTrue:
		[^ScriptSourceMirror new
			name: (name allButLast: 4);
			type: 'css'; 
			content: (streamContentsForTextResource: stream)].
		
	stream reset.
	(stream next: 'Smalltalk' size) = 'Smalltalk' asByteArray
		ifTrue: [^smalltalkSourceMirrors mirrorForStream: (transformStreamForSource: stream)].

	stream reset.
	(stream next: 'Newspeak' size) = 'Newspeak' asByteArray
		ifTrue: [^newspeakSourceMirrors mirrorForStream: (transformStreamForSource: stream)].
	^nil
)
streamContentsForTextResource: stream = (
	^(MultiByteBinaryOrTextStream
		with: stream contents asString
		encoding: 'utf8') reset contents
)
topLevelClasses = (
	^Root values select:
		[:each | each isBehavior
			and: [each language isNewspeakLanguage3]
			and: [each enclosingClass isNil]]
)
transformStreamForSource: stream = (
	#BOGUS. (* We should preserve source line endings, but doing so will require fixing many part of the system that assume CR line endings. *)
	^(MultiByteBinaryOrTextStream
		with: stream contents asString
		encoding: 'utf8') reset contents withSqueakLineEndings readStream
)
) : (
)
class NameOnlySourceMirror named: n actualMirror: m = (
(* A way to put off actually constructing a source mirror, which might involve expensive parsing. *)
|
	public name = n.
	public actualMirror <Delay[SourceMirror]> = m.
|) (
) : (
)
class ScriptSourceMirror = SourceMirror (|
	public name
	public type
	public content
|) (
public children = (
	^{}
)
public contentsEquals: other = (
	^type = other type and: [content = other content]
)
public filename = (
	^name, '.', type
)
public installAsToplevelClass = (
	Root at: name asSymbol put: (Script new type: type; content: content)
)
public isScriptMirror = (
	^true
)
public streamTo: stream <WriteStream> = (
	stream nextPutAll: content.
)
) : (
)
public class SourceMirror = (
) (
public contents = (
	^String streamContents: [:stream | streamTo: stream]
)
public contentsEquals: other= (

	^contents = other contents
)
public correspondsHash = (
	^name hash
)
public correspondsTo: other = (
	^name = other name
)
public source = (
	^contents
)
) : (
)
public class StringMirror = (
(* Encapsulates functionality for some leaf elements, e.g. category or class comment *)
|
	public string
|) (
public children = (
	^{}
)
public contentsEquals: other = (
	^string = other string
)
public correspondsHash = (
	^42
)
public correspondsTo: other = (
	^true
)
) : (
)
class TextSourceMirror = SourceMirror (|
	public name
	public contents
|) (
public children = (
	^{}
)
public contentsEquals: other = (
	^contents = other contents
)
public filename = (
	^name, '.txt'
)
public installAsToplevelClass = (
	Root at: name asSymbol put: contents
)
public isTextMirror = (
	^true
)
public streamTo: stream <WriteStream> = (
	stream nextPutAll: contents.
)
) : (
)
flatten: collection = (
	| result |
	result:: List new.
	collection do: [:each | result addAll: each].
	^result
)
public imageMirrorsNotCorrespondingTo: mirrors = (
	^mirrorLoader imageMirrorsNotCorrespondingTo: mirrors
)
public installMirrorsAsToplevelClasses: newMirrors = (
	mirrorLoader installMirrorsAsToplevelClasses: newMirrors
)
public methodMirrorForStream: stream ifError: errorBlock = (
	^mirrorLoader methodMirrorForStream: stream ifError: errorBlock
)
public mirrorForClassInImageCorrespondingTo: mirror ifPresent: presentBlock ifAbsent: absentBlock = (
	^mirrorLoader mirrorForClassInImageCorrespondingTo: mirror ifPresent: presentBlock ifAbsent: absentBlock
)
public mirrorForStream: stream name: name = (
	^mirrorLoader mirrorForStream: stream name: name
)
) : (
)
